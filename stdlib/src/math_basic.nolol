// This file contains basic definitions and macros for math
// Import using ' include "std/math" '

// The mathematical constant pi
define math_pi=3.141

// The mathematical constant e
define math_e=2.718

// Returns the absolute value of x.
macro math_abs(x) expr
	(x>=0)*x-(x<0)*x
end

// If x is >0, returns 1 if <0 return -1, otherwise returns 0
macro math_sign(x) expr
	(x>=0)-(x<=0)
end

// Returns the next lower integer to x
macro math_floor(x) expr
	x/1000*1000
end

// Returns x%m (even on basic chips)
macro math_mod(x,m) expr
    x-x/m/1000*1000*m
end

// Returns the smaller number of x and y
macro math_min(x, y) expr
    x + (y-x)*(x>y)
end

// Returns the larger number of x and y
macro math_max(x, y) expr
    x + (y-x)*(x<y)
end

// Clamps x to the range of min and max
macro math_clamp(x,min,max) expr
    math_min(math_max(x, min), max)
end

// Linearly interpolates between a and b based on f (100% of a when f=0 and 100% of b when f=1)
macro math_lerp(a, b, f) expr
    (a * (1 - f)) + (b * f)
end

// Inverse linear interpolation
// Returns a number between 0 and 1 based on f (0.0 when f=a and 1.0 when f=b)
macro math_invlerp(a, b, f) expr
    (f - a) / (b - a)
end
